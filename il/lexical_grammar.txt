program         : statement* EOF ;

declaration     : var_declaration | statement ;

statement       : expr_statement | print_statement ;

var_declaration : "let" IDENTIFIER ( "=" expression )? ";" ;

expr_statement  : expression ";" ;

print_statement : "print" expression ";" ;


expression      : assignment ;

assignment      : IDENTIFIER "=" assignment | equality ;

equality        : comparison ( ( "!=" | "==" ) comparison )* ;

comparison      : term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term            : factor ( ( "-" | "+" ) factor )* ;

factor          : unary ( ( "/" | "*" ) unary )* ;

unary           : ( "-" | "!" ) unary | primary ;

primary         : NUMBER | STRING | "true" | "false" | "null" | "(" expression ")" | IDENTIFIER ;
